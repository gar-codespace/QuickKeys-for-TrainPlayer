** rfid Version 1.0
** Generates a 12 digit random string for a stand in rfid, qr, or bar code number for each piece of rolling stock.
** Adds the id to a field called rfid attached to each piece of rolling stock.

Let valueList = (a, b, c, d, e, f, g, h, i, j, k, l, m, n, o, p, q, r, s, t, u, v, w, x, y, z, 0, 1, 2, 3, 4, 5, 6, 7, 8, 9)
Let rsIDs = $Layout(IDSet, CarIds)
Let rsCount = $Set(@rsIDs, count)
Let rfidRoster = ""

Echo Adding a random QR Code to each rolling stock...

While($String(@rsIDs, Length) <> 0)

    Let rsID = $String(@rsIDs, NextToken)
    If($Car(@rsID, AAR) = ET)
        Continue
    EndIf
    Let carLabel = $Car(@rsID, Label)
    Call makeRFID
    Echo @carLabel @rfid
    Let $Car(@rsID, "rfid") = @rfid
    Let rfidRoster = @rfidRoster + @carLabel + "," + @rfid + "\n"

EndWhile

Echo Rolling stock count: @rsCount

Let sFilePath = $DataDir + "\Reports\TrainPlayer Report - rfidRoster.txt"
Call $Write(@sFilePath, @rfidRoster)

Proc makeRFID

Let i = 0
Let rfid = ""
While(@i < 12)

    Let randomNumber = $Rand(0, 35)
    Let randomValue = $Set(@valueList, get, @randomNumber)
    Let rfid = @rfid + @randomValue
    Let i = @i + 1

EndWhile

EndProc
